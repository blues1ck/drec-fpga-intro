module axis_fifo #(
    parameter int DATA_WIDTH  = 32,
    parameter int DEPTH       = 8,
    parameter int TID_WIDTH   = 8,
    parameter int TDEST_WIDTH = 4,
    parameter int TUSER_WIDTH = 4
) (
    input  logic                      clk,
    input  logic                      rst_n,
    // Slave interface
    input  logic                      s_axis_tvalid,
    output logic                      s_axis_tready,
    input  logic [DATA_WIDTH-1:0]     s_axis_tdata,
    input  logic [(DATA_WIDTH/8)-1:0] s_axis_tstrb,
    input  logic [(DATA_WIDTH/8)-1:0] s_axis_tkeep,
    input  logic                      s_axis_tlast,
    input  logic [TID_WIDTH-1:0]      s_axis_tid,
    input  logic [TDEST_WIDTH-1:0]    s_axis_tdest,
    input  logic [TUSER_WIDTH-1:0]    s_axis_tuser,
    // Master interface
    output logic                      m_axis_tvalid,
    input  logic                      m_axis_tready,
    output logic [DATA_WIDTH-1:0]     m_axis_tdata,
    output logic [(DATA_WIDTH/8)-1:0] m_axis_tstrb,
    output logic [(DATA_WIDTH/8)-1:0] m_axis_tkeep,
    output logic                      m_axis_tlast,
    output logic [TID_WIDTH-1:0]      m_axis_tid,
    output logic [TDEST_WIDTH-1:0]    m_axis_tdest,
    output logic [TUSER_WIDTH-1:0]    m_axis_tuser
);

    localparam int ADDR_WIDTH  = $clog2(DEPTH);
    localparam int TSTRB_WIDTH = DATA_WIDTH / 8;

    typedef struct packed {
        logic [DATA_WIDTH-1:0]   tdata;
        logic [TSTRB_WIDTH-1:0]  tstrb;
        logic [TSTRB_WIDTH-1:0]  tkeep;
        logic                    tlast;
        logic [TID_WIDTH-1:0]    tid;
        logic [TDEST_WIDTH-1:0]  tdest;
        logic [TUSER_WIDTH-1:0]  tuser;
    } fifo_entry_t;

    // FIFO memory array
    fifo_entry_t mem[0:DEPTH-1];

    // FIFO control signals
    logic [ADDR_WIDTH-1:0] wr_ptr;
    logic [ADDR_WIDTH-1:0] rd_ptr;
    logic [ADDR_WIDTH:0]   count;

    logic full;
    logic empty;
    logic wait_clk_after_rst_n;
    logic wr_en;
    logic rd_en;

    // FIFO status flags
    assign full  = (count == (ADDR_WIDTH+1)'(DEPTH));
    assign empty = (count == '0);

    // AXI Stream ready/valid signals
    assign s_axis_tready = !full & !wait_clk_after_rst_n;
    assign m_axis_tvalid = !empty & !wait_clk_after_rst_n;

    // Write and read enable signals
    assign wr_en = s_axis_tvalid & s_axis_tready & !full;
    assign rd_en = m_axis_tvalid & m_axis_tready & !empty;

    // Output data assignments
    assign m_axis_tdata = mem[rd_ptr].tdata;
    assign m_axis_tstrb = mem[rd_ptr].tstrb;
    assign m_axis_tkeep = mem[rd_ptr].tkeep;
    assign m_axis_tlast = mem[rd_ptr].tlast;
    assign m_axis_tid   = mem[rd_ptr].tid;
    assign m_axis_tdest = mem[rd_ptr].tdest;
    assign m_axis_tuser = mem[rd_ptr].tuser;

    // Write pointer and FIFO memory write
    always_ff @(posedge clk or negedge rst_n) begin
        if (!rst_n) begin
            wr_ptr <= '0;
        end else if (wr_en) begin
            mem[wr_ptr].tdata <= s_axis_tdata;
            mem[wr_ptr].tstrb <= s_axis_tstrb;
            mem[wr_ptr].tkeep <= s_axis_tkeep;
            mem[wr_ptr].tlast <= s_axis_tlast;
            mem[wr_ptr].tid   <= s_axis_tid;
            mem[wr_ptr].tdest <= s_axis_tdest;
            mem[wr_ptr].tuser <= s_axis_tuser;
            wr_ptr            <= wr_ptr + 1;
        end
    end

    // Read pointer update
    always_ff @(posedge clk or negedge rst_n) begin
        if (!rst_n) begin
            rd_ptr <= '0;
        end else if (rd_en) begin
            rd_ptr <= rd_ptr + 1;
        end
    end

    // FIFO occupancy counter
    always_ff @(posedge clk or negedge rst_n) begin
        if (!rst_n) begin
            count <= '0;
        end else begin
            unique case ({wr_en, rd_en})
                2'b10: count <= count + 1; // Write only
                2'b01: count <= count - 1; // Read only
                default: count <= count;   // Simultaneous read/write or no changes
            endcase
        end
    end

    // Ensure TREADY and TVALID are 0 after reset and before first clock edge
    always_ff @(posedge clk or negedge rst_n) begin
        if (!rst_n) begin
            wait_clk_after_rst_n <= 1'b1;
        end else begin
            wait_clk_after_rst_n <= 1'b0;
        end
    end

endmodule
